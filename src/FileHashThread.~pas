unit FileHashThread;

interface

uses
  Classes, Crypt;

type
  TCalculatedHashEvent = procedure(Sender: TThread; const AHash: string) of object;

  { TFileHashThread }
  TFileHashThread = class(TThread)
  private
    FHash: THash;
    FBytes, FBytesRead: Cardinal;
    FOnStart, FOnProgress: TProgressEvent;
    FOnFinish: TCalculatedHashEvent;
    FFileName, FHashValue: string;
    procedure DoNotifyOnStart();
    procedure DoNotifyOnFinish();
    procedure DoNotifyOnProgress();
    procedure OnStartHashing(Sender: TObject; const AFileSize: Cardinal);
    procedure OnHashing(Sender: TObject; const ABytesRead: Cardinal);
  protected
    procedure Execute; override;
  public
    constructor Create(const AFileName: string; AHashAlgorithm: THashAlgorithm);
    destructor Destroy; override;
    { external }
    property OnFinish: TCalculatedHashEvent read FOnFinish write FOnFinish;
    property OnProgress: TProgressEvent read FOnProgress write FOnProgress;
    property OnStart: TProgressEvent read FOnStart write FOnStart;
  end;

implementation

{ TFileHashThread }

{ public TFileHashThread.Create

  Constructor for creating a TFileHashThread instance. }

constructor TFileHashThread.Create(const AFileName: string;
  AHashAlgorithm: THashAlgorithm);
begin
  inherited Create(True);
  FreeOnTerminate := True;
  FFileName := AFileName;
  FHash := THash.Create(AHashAlgorithm);

  with FHash do
  begin
    OnStart := OnStartHashing;
    OnProgress := OnHashing;
  end;  //of with
end;

{ public TFileHashThread.Create

  Destructor for destroying a TFileHashThread instance. }

destructor TFileHashThread.Destroy;
begin
  FHash.Free;
  inherited Destroy;
end;

procedure TFileHashThread.DoNotifyOnStart;
begin
  if Assigned(FOnStart) then
    FOnStart(Self, FBytes);
end;

procedure TFileHashThread.DoNotifyOnFinish;
begin
  if Assigned(FOnFinish) then
    FOnFinish(Self, FHashValue);
end;

procedure TFileHashThread.DoNotifyOnProgress;
begin
  if Assigned(FOnProgress) then
    FOnProgress(Self, FBytesRead);
end;

procedure TFileHashThread.OnStartHashing(Sender: TObject; const AFileSize: Cardinal);
begin
  FBytes := AFileSize;
  Synchronize(DoNotifyOnStart);
end;

procedure TFileHashThread.OnHashing(Sender: TObject; const ABytesRead: Cardinal);
begin
  FBytesRead := ABytesRead;
  Synchronize(DoNotifyOnProgress);
end;

{ protected TFileHashThread.Execute

  Calculates a hash from a file. }

procedure TFileHashThread.Execute;
begin
  FHashValue := FHash.HashFile(FFileName);
  Synchronize(DoNotifyOnFinish);
end;

end.
 